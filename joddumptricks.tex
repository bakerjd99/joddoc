\section{Turning JOD Dump Script Tricks}\label{ap:joddumptricks}

Dump script\index{dump scripts} generation is my favorite JOD feature. Dump scripts serialize 
JOD dictionaries; they are mainly used to back up dictionaries and interact with 
version control systems: see appendix~\ref{ap:jodvcsys} on page~\pageref{ap:jodvcsys}.
However, dump scripts are general J scripts and can do much more!  
Maintaining a stable of healthy JOD dictionaries is easier 
if you can turn a few dump script tricks.\footnote{Spicing up one's rhetoric with a double entendre 
like ``turning tricks'' might be construed as a 
\href{http://thefederalist.com/2015/03/24/microaggressions-and-trigger-warnings-meet-real-trauma/}{\emph{microaggression}}. 
The point of colorful language is to memorably make a point. 
You are unlikely to forget \emph{turning dump script tricks.} 
} The following examples assume a configured folder \verb|JODTEST|: see Section~\ref{ss:jodcfgdesc} on~\pageref{ss:jodcfgdesc}.

\begin{enumerate}

\item \textbf{Flattening reference paths:} Open JOD dictionaries define a reference path: see appendix~\ref{ap:refpath} on page~\pageref{ap:refpath}.
For example, if you open the following dictionaries:

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. open four dictionaries
   od ;:'smugdev smug image utils'
+-+-----------------------+-------+----+-----+-----+
|1|opened (ro/ro/ro/ro) ->|smugdev|smug|image|utils|
+-+-----------------------+-------+----+-----+-----+
\end{lstlisting}

The reference path\index{reference path} is \texttt{/smugdev/smug/image/utils}.

When objects are retrieved each dictionary on the path is searched in reference path order.
If there are \emph{no compelling reasons} to maintain separate dictionaries you can improve
JOD retrieval performance and simplify dictionary maintenance by flattening all or part of the path. 

To flatten the reference path do:

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. reopen the first three dictionaries on the path
   od ;:'smugdev smug image' [ 3 od ''
+-+--------------------+-------+----+-----+
|1|opened (ro/ro/ro) ->|smugdev|smug|image|
+-+--------------------+-------+----+-----+

   NB. dump to a temporary file (df)
   df=: {: showpass make jpath '~JODTEST/smugflat.ijs'
+-+---------------------------+----------------------------+
|1|object(s) on path dumped ->|c:/jodtest/test/smugflat.ijs|
+-+---------------------------+----------------------------+

   NB. create a new flat dictionary
   newd 'smugflat';jpath '~JODTEST/smugflat' [ 3 od ''
+-+---------------------+--------+-------------------------+
|1|dictionary created ->|smugflat|c:/jodtest/test/smugflat/|
+-+---------------------+--------+-------------------------+
\end{lstlisting}

\newpage

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. open the flat dictionary and (utils)
   od ;:'smugflat utils'
+-+-----------------+--------+-----+
|1|opened (rw/ro) ->|smugflat|utils|
+-+-----------------+--------+-----+
  
   NB. reload dump script ... output not shown ...  
   0!:0 df
\end{lstlisting}

The collapsed path \texttt{/smugflat/utils} will return the same objects as the longer path.
It is important to understand that the collapsed dictionary \texttt{smugflat} does not necessarily contain
the same objects found in the three original dictionaries \texttt{smugdev}, \texttt{smug} and \texttt{image}.
If objects with the same name exist in the original dictionaries only the first one found will 
be in the collapsed dictionary.


\item \textbf{Merging dictionaries:} If two dictionaries \emph{contain no overlapping objects} it might make
sense to merge them. This is easily achieved with dump scripts. To merge two or more dictionaries do:

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. open and dump first dictionary
   od 'dict0' [ 3 od ''
+-+--------------+-----+
|1|opened (rw) ->|dict0|
+-+--------------+-----+
   df0=: {: showpass make jpath '~JODTEST/dict0.ijs'
+-+---------------------------+-------------------------+
|1|object(s) on path dumped ->|c:/jodtest/test/dict0.ijs|
+-+---------------------------+-------------------------+
   
   NB. open and dump second dictionary
   od 'dict1' [ 3 od ''
+-+--------------+-----+
|1|opened (rw) ->|dict1|
+-+--------------+-----+
   df1=: {: showpass make jpath '~JODTEST/dict1.ijs'
+-+---------------------------+-------------------------+
|1|object(s) on path dumped ->|c:/jodtest/test/dict1.ijs|
+-+---------------------------+-------------------------+   
  
   NB. create new merge dictionary
   newd 'mergedict';jpath '~JODTEST/mergedict' [ 3 od ''
+-+---------------------+---------+--------------------------+
|1|dictionary created ->|mergedict|c:/jodtest/test/mergedict/|
+-+---------------------+---------+--------------------------+
\end{lstlisting}

   \newpage
   
\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]   
   NB. open merge dictionary and run dump scripts
   od 'mergedict'
+-+--------------+---------+
|1|opened (rw) ->|mergedict|
+-+--------------+---------+

   NB. reload dump scripts ... output not shown ...  
   0!:0 df0  
   0!:0 df1
\end{lstlisting}

Be careful when merging dictionaries. If there are common objects the last object loaded is the one
retained in the merged dictionary.


\item \textbf{Updating master file parameters:} When a new parameter\index{dictionary!parameters} is added to \texttt{jodparms.ijs}, 
see appendix~\ref{ap:jodparms} on page~\pageref{ap:jodparms}, it will not be available in existing dictionaries.
With dump scripts you can rebuild existing dictionaries and update parameters. To rebuild a dictionary with
new or custom parameters do:

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. save current dictionary registrations
   (toHOST ; 1 { 5 od '') write_ajod_ jpath '~JODTEST/jodregister.ijs'
   
   NB. open dictionary requiring parameter update 
   od 'dict0' [ 3 od ''
+-+--------------+-----+
|1|opened (rw) ->|dict0|
+-+--------------+-----+
   
   NB. dump dictionary and close
   df=: {: showpass make jpath '~JODTEST/dict0.ijs'
+-+---------------------------+-------------------------+
|1|object(s) on path dumped ->|c:/jodtest/test/dict0.ijs|
+-+---------------------------+-------------------------+

   3 od ''
+-+---------+-----+
|1|closed ->|dict0|
+-+---------+-----+

   NB. erase master file and JOD object id file
   ferase jpath '~addons/general/jod/jmaster.ijf'
1
   ferase jpath '~addons/general/jod/jod.ijn'
1
\end{lstlisting}

\newpage

\begin{lstlisting}[frame=single,framerule=0pt,basicstyle=\ttfamily\footnotesize]
   NB. recycle JOD - this recreates (jmaster.ijf) and (jod.ijn) 
   NB. using the new dictionary parameters defined in (jodparms.ijs)   
   (jodon , jodoff) 1
1 1

   NB. re-register dictionaries
   load jpath '~JODTEST/jodregister.ijs'

   NB. create a new dictionary - it will have the new parameters
   newd 'dict0new'
   
   NB. reload dump script ... output not shown ...
   0!:0 df  
\end{lstlisting}

Before executing complex dump script procedures \emph{back up your JOD dictionary folders} and play with 
dump scripts on test dictionaries. Dump scripts are essential JOD dictionary maintenance tools but like
all powerful tools they must be used with care. 


\end{enumerate} 

